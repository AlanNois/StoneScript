// Copyright © 2019, Matjaž Guštin <dev@matjaz.it> <https://matjaz.it>
// Released under the BSD 3-clause license.

// Loadout 1: ranged, e.g. double crossbow
// Loadout 2: single-handed melee with dashing shield
// Loadout 3: armor-crushing, e.g. 2x warhammer
// Loadout 4: 2x single-handed melee close quarters
//            e.g. dual-wielding swords         

// Player's face
>( O_*


// --- Rocky plateau ---
?loc = rocky
  equip shovel
  ?pickup = stone
    equip star  // quick picking up
  // Dysangelos
  ?foe = dysangelos_bearer
    loadout 4
  ?foe = dysangelos_elementalist
    loadout 4
    // TODO use the proper runes against
    // the current rune in the eye of the boss
  ?foe = dysangelos_perfected
    loadout 4
    ?foe.armor > 0
      loadout 3
    ?hp < 31
      // Dsyangleos' most powerful move deals
      // 30 damage points on 5* difficulty
      activate potion
 
 

// --- Deadwood Canyon ---
?loc = valley
  equipR hatchet
  equipL ouroboros  // healing
  ?pickup = wood
    equipR star  // quick picking up
    equipL ouroboros  // healing
  ?foe = mosquito
    loadout 1
  ?foe = scarab
    loadout 4
  ?foe.distance < 10
    loadout 4
  // Xyaloalgia, residue of Leuce
  ?foe = tree_boss
    loadout 4



// --- Caves of Fear ---
?loc = caves
  equipL ouroboros
  ?pickup = tar
    equipR star  // quick picking up
    equipL ouroboros  // healing
  ?foe = bat
    loadout 1
  ?foe = spider
    loadout 1
  ?foe = eggs
    loadout 1
  ?foe.distance < 10
    loadout 4
  // Bolesh, the cunning
  ?foe = boss
    loadout 4



// --- Mushroom forest ---
?loc = fungus
  equip poison staff
  // Angry shroom
  ?foe = boss
    equip poison staff
  // xxx, the sporeadic
  ?foe = boss_fat
    loadout 4
  // xxx, fungi to be with
  ?foe = boss_skinny
    loadout 4



// --- Haunted halls ---
?loc = crypt
  equip vigor staff
  // Pallas, the skinless
  ?foe = boss
    equip vigor staff



// --- Boiling mine ---
?loc = mine
  equipL ouroboros
  ?pickup = bronze
    equipR star  // quick picking up
    equipL ouroboros  // healing
  ?foe = master
    loadout 1
  ?foe = elemental
    loadout 1
  ?foe = walker
    loadout 2
    ?foe.distance < 10
      loadout 3

// --- Bronze guardian ---
?loc = bronze_guardian
  equipL ouroboros
  ?time > 600
    loadout 1
  ?time > 960 & time < 980
    loadout 2
  ?time > 980
    loadout 4
  ?time > 980 & foe.armor > 0
    loadout 3
  ?time > 1100
    loadout 1



// --- Icy ridge ---
?loc = icy
  equipL ouroboros
  ?foe = elemental
    equip fire staff
  ?foe = pillar
    equip fire staff
  ?foe = ki
    equip fire staff
  ?foe.distance < 10
    equip fire staff
  // Hrimnir
  ?foe = yeti
    ?foe.distance > 5
      equip dashing shield
    ?foe.distance > 2
      // The shovel has an attack range of 1
      // so it forces the player to move very
      // very close. So close that it can avoid
      // the attacks (snowballs)
      equip shovel
    ?foe.distance < 3
      loadout 4



// --- Temple ---
?loc = temple
  equip ice staff



// --- Nagaraja ---
?loc = nagaraja
  loadout 4



// --- General rules ---
// Apply health potion on low health
?hp < 5
  activate potion

// Equip Experience stone to kill a boss
// and get double XP from the kill
?foe = boss & foe.armor = 0 & foe.hp < 25
  equipR sword
  equipL experience